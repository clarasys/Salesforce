/*
Name:  CapacityPlanningController_TestSuite.cls
Copyright Â© 2015  Clarasys
=========================================================================================================
CLASS DESCRIPTION
=========================================================================================================
-------
Unit test suite for the SprintCapacityPlanningControllerExt Class
-------
=========================================================================================================
HISTORY
------- 
Ver. Author         Date        Detail
0.1  Clarasys       2015-12-22  Initial class development
*/

@isTest
public with sharing class CapacityPlanningController_TestSuite {

/*
=====================
Positive Test Cases
=====================
*/

 /*
  * Method: test_SprintCapacityControllerExt_AddTeamMemberCapacity to test SprintCapacityPlanningControllerExt
  * Test Data Setup: sprinter__Project__c, sprinter__Sprint__c with associated clarasys_Scrum_Team__c with one clarasys_Scrum_Team_Member__c with no capacity, sprinter__User_Story__c with one sprinter__Element__c 
  * Test Conditions: Insert the test data and then instantiate the Controller, update the Scrum Team Member Capacity and call the updateSprintCapacity() action  
  * Expected Outcome: the clarasys_Scrum_Team_Member__c.Capacity__c is updated to the new value entered, a new clarasys_Sprint_Scrum_Team_Member__c record is added to the Sprint (associted with the clarasys_Scrum_Team_Member__c)
  */
  
    static testmethod void test_SprintCapacityControllerExt_AddTeamMemberCapacity() {
        
        /*================================================================================================
        // -- TEST DATA --
        // [Single Project, Single Sprint (Status = 'Active') with single User Story (with a single Task)]
        // [Scrum Team (linked to the Sprint), with single Scrum Team Member (without Capacity) linked to a sprinter__Team_Member__c]
        //==============================================================================================*/ 
    
        //create Project
        sprinter__Project__c testProject = new sprinter__Project__c();
          testProject.name = 'Test Project';
          testProject.sprinter__Status__c = 'Active';
          testProject.sprinter__Description__c = 'Test Project Description';
        
        insert testProject; //insert testProject record
        
        //create Scrum Team
        clarasys_Scrum_Team__c testScrumTeam = new clarasys_Scrum_Team__c();
            testScrumTeam.name = 'Test Scrum Team';
            testScrumTeam.Project__c = testProject.Id;
        
        insert testScrumTeam; //insert testScrumTeam record
        
        //create Sprint
        sprinter__Sprint__c testSprint  = new sprinter__Sprint__c();
          testSprint.sprinter__Project__c = testProject.Id;
          testSprint.Scrum_Team__c = testScrumTeam.Id;
          testSprint.sprinter__Start_Date__c = Date.Today(); // new sprint
          testSprint.sprinter__End_Date__c = testSprint.sprinter__Start_Date__c + 10;
          testSprint.sprinter__Status__c = 'Active';
        
        insert testSprint; //insert testSprint record
        
        //create User Story
        sprinter__User_Story__c testStory = new sprinter__User_Story__c();
            testStory.sprinter__Project__c = testProject.Id;
            testStory.sprinter__Sprint__c = testSprint.Id;
            testStory.sprinter__Status__c = 'Ready';
          
        insert testStory; //insert testStory record
        
        //create Team Member
        sprinter__Team_Member__c testTeamMember = new sprinter__Team_Member__c();
            testTeamMember.name = 'Kristian Jenner';
        
        insert testTeamMember; //insert testTeamMember record
        
        //create Scrum Team Member
        clarasys_Scrum_Team_Member__c testScrumTeamMember = new clarasys_Scrum_Team_Member__c();
            testScrumTeamMember.Team_Members__c = testTeamMember.Id;
            testScrumTeamMember.Scrum_Team__c = testScrumTeam.Id;
            testScrumTeamMember.Scrum_Role__c = 'Team Member';
            testScrumTeamMember.Capacity__c = 0;
            
        insert testScrumTeamMember; //insert testScrumTeamMember record
        
        //create Task
        sprinter__Element__c testTask = new sprinter__Element__c();
            testTask.sprinter__User_Story__c = testStory.Id;
            testTask.sprinter__Owner__c = testTeamMember.Id;
            testTask.sprinter__Type__c = 'Task';
            testTask.sprinter__Status__c = 'In Progress';
            testTask.Effort__c = 10;
        
        insert testTask; //insert testTask record
        
        /*================================================================================================
        // -- TEST STEPS --
        // 1) instantiate the sprinter__Sprint__c standard controller and SprintCapacityPlanningControllerExt controller extension for the testSprint record
        // 2) set current page to VF page sprinter_SprintCapacityPlanning and seed id parameter with the testSprint.Id
        // 3) call the getSprint() getter and verify that the sprint returned is the testSprint
        // 4) call the getSprintCapacityZero() getter and verify that it returns True
        // 5) call the getNoAssociatedScrumTeam() getter and verify that it returns False
        // 6) call the getDisplayScrumTeamMembers() getter and verify that there is one DisplayScrumTemMember returned and that it is the same as the testScrumTeamMember
        // 7) set the scrumTeamMemberUpdatedCapacity = 10 for the DisplayScrumTeamMember and verify that the scrumTeamMemberUpdatedCapacity is updated
        // 8) verify that there are no clarasys_Sprint_Scrum_Team_Member__c records associated with the Sprint then call the updateSprintCapacity action method and verify that one has been added and the Sprint Capacity is updated
        //==============================================================================================*/ 
        
        test.startTest(); //start Test
        
        // [1]
        ApexPages.StandardController sc = new ApexPages.standardController(testSprint);
        SprintCapacityPlanningControllerExt e = new SprintCapacityPlanningControllerExt(sc);
        System.assertEquals(e.s, testSprint);
        
        // [2]
        PageReference pageRef = Page.sprinter_SprintCapacityPlanning;
        Test.setCurrentPage(pageRef);
        ApexPages.currentPage().getParameters().put('id', testSprint.Id);
        
        // [3]
        sprinter__Sprint__c sprint = e.getSprint();
        System.assertEquals(sprint.Id, testSprint.Id);
        
        // [4]
        Boolean ZeroCapacity = e.getSprintCapacityZero();
        System.assertEquals(True, ZeroCapacity);
        
        // [5]
        Boolean NoScumTeam = e.getNoAssociatedScrumTeam();
        System.assertEquals(False, NoScumTeam);
        
        // [6]
        List<SprintCapacityPlanningControllerExt.DisplayScrumTeamMember> lstDisplayScrumTeamMember = e.getDisplayScrumTeamMembers();
        System.assertEquals(1, lstDisplayScrumTeamMember.size());
        System.assertEquals(testScrumTeamMember.Id, lstDisplayScrumTeamMember[0].scrumTeamMemberId);
        System.assertEquals(testScrumTeamMember.Capacity__c, lstDisplayScrumTeamMember[0].scrumTeamMemberCapacity);
        System.assertEquals(testTeamMember.name, lstDisplayScrumTeamMember[0].scrumTeamMemberName);
        System.assertEquals(testSprint.Id, lstDisplayScrumTeamMember[0].scrumTeamMemberSprintId);
        System.assertEquals(testTask.Effort__c, lstDisplayScrumTeamMember[0].scrumTeamMemberAssignedEffort);
        System.assertEquals(null, lstDisplayScrumTeamMember[0].scrumTeamMemberUpdatedCapacity);
        
        // [7]
        e.lstDisplayScrumTeamMember[0].scrumTeamMemberUpdatedCapacity = 10;
        e.setDisplayScrumTeamMembersTotalCapacity();
        List<SprintCapacityPlanningControllerExt.DisplayScrumTeamMember> lstUpdatedDisplayScrumTeamMember = e.getDisplayScrumTeamMembers();
        System.assertEquals(10, lstDisplayScrumTeamMember[0].scrumTeamMemberUpdatedCapacity);
        System.assertEquals(10, e.displayScrumTeamMembersTotalCapacity);
      
        // [8]
        List<clarasys_Sprint_Scrum_Team_Member__c> lstSprintScumTeamMemberBeforeUpdate = new List<clarasys_Sprint_Scrum_Team_Member__c>([Select Id
                                                                                                                                         FROM clarasys_Sprint_Scrum_Team_Member__c
                                                                                                                                         WHERE Sprint__c = :testSprint.Id
                                                                                                                                        ]);
        System.assertEquals(0, lstSprintScumTeamMemberBeforeUpdate.size());
        
        PageReference newPage1 = e.updateSprintCapacity(); //call the updateSprintCapacity() action
        
        List<clarasys_Sprint_Scrum_Team_Member__c> lstSprintScumTeamMemberAfterUpdate = new List<clarasys_Sprint_Scrum_Team_Member__c>([Select Id, Scrum_Team__c, Scrum_Team_Member__c, Scrum_Team_Member_Name__c, Capacity__c 
                                                                                                                                        FROM clarasys_Sprint_Scrum_Team_Member__c
                                                                                                                                        WHERE Sprint__c = :testSprint.Id
                                                                                                                                       ]);
        System.assertEquals(1, lstSprintScumTeamMemberAfterUpdate.size());
        System.assertEquals(10, lstSprintScumTeamMemberAfterUpdate[0].Capacity__c);
        
        List<sprinter__Sprint__c> lstUpdatedSprint = new List<sprinter__Sprint__c>([SELECT Id, Sprint_Scrum_Capacity__c
                                                                                    FROM sprinter__Sprint__c
                                                                                    WHERE Id = :testSprint.Id
                                                                                   ]);
        
        System.assertEquals(10, lstUpdatedSprint[0].Sprint_Scrum_Capacity__c);
       
        test.stopTest(); //stop Test
    }

/*
========================
Test Data Helper Methods
========================
*/



}
